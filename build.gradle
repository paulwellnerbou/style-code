plugins {
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'de.wellnerbou'
version = '0.4'

mainClassName = "de.wellnerbou.stylecode.app.StyleCodeApp"

sourceCompatibility = 1.7
targetCompatibility = 1.7

run {
    if ( project.hasProperty('args') ) {
        args project.args.split('\\s+')
    }
}

// user and key configuration for bintray deployment:
// ./gradlew bintrayUpload -PpublishUser=<USER> -PpublishKey=<KEY>
if (!project.hasProperty("publishUser")) {
    ext.publishUser = ""
}
if (!project.hasProperty("publishKey")) {
    ext.publishKey = ""
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'stylecode'
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'
    compile 'org.pegdown:pegdown:1.6.0'
    compile 'com.lexicalscope.jewelcli:jewelcli:0.8.9'
    compile 'com.google.guava:guava:19.0'
    // Java 8:
    // compile 'com.github.spullara.mustache.java:compiler:0.9.1'
    // Java 7:
    compile 'com.github.spullara.mustache.java:compiler:0.8.1'
    compile 'org.jsoup:jsoup:1.7.2'
    // Java 7:
    testCompile 'org.assertj:assertj-core:2.4.1'
    testCompile 'junit:junit:4.+'
}

bintray {
    user = "$publishUser"
    key = "$publishKey"
    publications = ['mavenJava']

    dryRun = false
    publish = true

    pkg {
        repo = 'maven'
        name = 'stylecode'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/paulwellnerbou/style-code'
    }
}
